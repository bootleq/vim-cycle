Describe Group definition
  Before each
    new
  End

  After each
    bwipeout!
  End

  Describe Default groups
    Before each
      call cycle#test#reset_default_groups()
    End

    It has default group yes/no
      call setline(1, 'No')
      execute 'normal \a'
      Assert Equals(getline(1), 'Yes')
    End

    It can overwrite default groups
      call cycle#test#reset_default_groups([
            \   [['0', '2']], [['no', 'foo']]
            \ ])

      call setline(1, 'Yes No')

      execute 'normal \a'
      Assert Equals(getline(1), 'Yes No', 'Expect default "yes" deleted')

      call search('No', 'w')
      execute 'normal \a'
      Assert Equals(getline(1), 'Yes Foo')
    End

    It adds default groups for filetype
      call cycle#test#clear_all_groups()
      let g:cycle_default_groups_for_lua = [[['~=', '==']]]
      call cycle#test#reinitialize_groups()

      let line = 'local h == 1 and 2 or 7'
      let col = stridx(line, '==') + 1
      call setline(1, line)

      call cursor(1, col)
      execute 'normal \a'
      Assert Equals(getline(1), 'local h == 1 and 2 or 7', 'Expect unchaged, filetype mismatch')

      setlocal filetype=lua
      Assert Equals(&filetype, 'lua')

      call cursor(1, col)
      execute 'normal \a'
      Assert Equals(getline(1), 'local h ~= 1 and 2 or 7')
    End

    It link_filetypes: link multiple filetypes
      call cycle#test#clear_all_groups()
      let g:cycle_default_groups_for_lua = [[['foo', 'bar']]]
      let g:cycle_filetype_links = {
            \   'text': 'asm',
            \   'xxx': 'lua',
            \ }
      call cycle#test#reinitialize_groups()

      let line = 'foo'
      call setline(1, line)

      setlocal filetype=text
      Assert Equals(&filetype, 'text')
      execute 'normal \a'
      Assert Equals(getline(1), 'foo', 'Expect unchaged, filetype mismatch')

      setlocal filetype=xxx
      Assert Equals(&filetype, 'xxx')
      execute 'normal \a'
      Assert Equals(getline(1), 'bar')
    End
  End

  Context ad-hoc groups
    It cycle#parse_group()
      let group = cycle#parse_group([['Yes', 'No']])
      Assert Equals(group, #{items: ['Yes', 'No'], options: {}})

      let group = cycle#parse_group([['Yes', 'No'], 'match_case'])
      Assert Equals(group, #{items: ['Yes', 'No'], options: #{match_case: 1}})

      let groups = cycle#parse_group([['a:b', 'c:d'], 'sub_pairs'])
      Assert IsList(groups)
      Assert Equals(groups[0], #{items: ['b', 'd'], options: #{sub_pair: 1, begin_with: ['a', 'c']}})
      Assert Equals(groups[1], #{items: ['a', 'c'], options: #{sub_pair: 1, end_with: ['b', 'd']}})
    End
  End
End
